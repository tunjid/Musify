plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 34
    namespace "com.example.musify"

    defaultConfig {
        applicationId "com.example.musify"
        minSdk 23
        targetSdk 35
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        // load fields from local.properties file
        Properties properties = new Properties()
        properties.load(project.rootProject.file("local.properties").newDataInputStream())
        buildConfigField "String", "SPOTIFY_CLIENT_ID", "\"${properties.getProperty("SPOTIFY_CLIENT_ID")}\""
        buildConfigField "String", "SPOTIFY_CLIENT_SECRET", "\"${properties.getProperty("SPOTIFY_CLIENT_SECRET")}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        // To enable the use of java.time api's with minSdk < 26
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
        freeCompilerArgs = ["-Xcontext-receivers"]
    }
    buildFeatures {
        compose true
        buildConfig = true
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    // core
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.4'

    // java.time support for api's < Android O
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'

    // compose
    def composeBom = platform('androidx.compose:compose-bom:2024.06.00')
    implementation composeBom
    androidTestImplementation composeBom

    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.material:material"
    implementation "androidx.compose.ui:ui-util"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.foundation:foundation"
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"
    implementation 'androidx.activity:activity-compose:1.9.1'
    implementation "androidx.navigation:navigation-compose:2.7.7"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0-RC"

    //accompanist
    implementation "com.google.accompanist:accompanist-placeholder-material:0.13.0"

    // image handling
    implementation "io.coil-kt:coil-compose:2.4.0"
    // lottie compose
    implementation "com.airbnb.android:lottie-compose:5.2.0"

    // testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    testImplementation "org.robolectric:robolectric:4.11.1"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.8.1"
    testImplementation 'org.mockito:mockito-core:5.10.0'
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.1.0"

    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'
    // Jackson support for kotlin types
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.2'

    // hilt
    implementation "com.google.dagger:hilt-android:2.51"
    implementation 'androidx.hilt:hilt-navigation-compose:1.2.0'
    kapt "com.google.dagger:hilt-compiler:2.51"

    //exoplayer
    implementation 'com.google.android.exoplayer:exoplayer-core:2.19.1'
    // for PlayerNotificationManager
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.19.1'

    // paging
    implementation "androidx.paging:paging-runtime-ktx:3.3.2"
    implementation "androidx.paging:paging-compose:3.3.2"

    // color extraction
    implementation 'androidx.palette:palette-ktx:1.0.0'

    // mdc components for view
    // This dependency is not needed once the min lines param
    // for Text composable becomes available in compose
    // This parameter is needed in the home screen
    implementation 'com.google.android.material:material:1.12.0'

    // tiling
    implementation "com.tunjid.tiler:tiler:1.1.0-rc02"
    implementation "com.tunjid.tiler:compose:1.1.0-rc02"

    // state production
    implementation "com.tunjid.mutator:core:1.1.0"
    implementation "com.tunjid.mutator:coroutines:1.1.0"
}